/* Grammar Tool for LL(1) Grammars
 *
 * Copyright (C) 2015 Benjamin Kowarsch
 *
 * Synopsis
 *
 * GLL is a tool to verify and visualise LL(1) grammars.  It parses EBNF
 * specifications of grammars, calculates FIRST and FOLLOW sets, reports
 * any LL(1) violations and generates (railroad) syntax diagrams.
 *
 * File
 *
 * grammar.gll
 *
 * EBNF grammar of GLL input files.
 *
 * License
 *
 * GLL is free software: you can redistribute it and/or modify it under the
 * terms of the GNU Lesser General Public License Version 2 (LGPL2) as
 * published by the Free Software Foundation.
 *
 * GLL is distributed in the hope that it will be useful,  but WITHOUT
 * ANY WARRANTY; without even the implied warranty of MERCHANTABILITY or
 * FITNESS FOR A PARTICULAR PURPOSE.  Refer to the license for details.
 *
 * You should have received a copy of the GNU Lesser General Public License
 * along with GLL.  If not, visit <http://www.gnu.org/licenses/>.
 */

grammar gll;

/*** Reserved Words ***/

reserved
  GRAMMAR = 'grammar',
  RESERVED = 'reserved',
  ALIAS = 'alias',
  ENDG = 'endg';


/*** Non-Terminal Symbols ***/

/* Grammar Specification */

specification :=
  GRAMMAR grammarIdent ';'
  ( RESERVED reswordList )?
  ( definition ';' )+
  ENDG grammarIdent '.'
  EndOfFile
  ;

alias grammarIdent = ident;


/* Identifier */

ident :=
  NonTerminalIdent | TerminalIdent
  ;


/* Definition */

definition :=
  ALIAS aliasDef |
  nonTerminalDef |
  terminalDef |
  fragmentDef |
  nonSemanticDef
  ;


/* Reserved Word Definition List */

reswordList :=
  reswordDef ( ',' reswordDef )*
  ;


/* Reserved Word Definition */

reswordDef :=
  ReswordIdent ( '=' String )?
  ;


/* Alias Definition */

aliasDef :=
  nonTermAliasDef | termAliasDef
  ;


/* Non-Terminal Alias Definition */

nonTermAliasDef :=
  NonTermAlias ( ',' NonTermAlias )* '=' NonTermAliasValue
  ;

alias NonTermAlias, NonTermAliasValue = NonTerminalIdent;


/* Terminal Alias Definition */

termAliasDef :=
  TerminalAlias ( ',' TerminalAlias )* '=' termAliasValue
  ;

alias TerminalAlias = TerminalIdent;



/* Terminal Alias Value */

termAliasValue :=
  TerminalIdent | literal
  ;


/* literal */

literal :=
  String | QuotedLowerLetter | QuotedUpperLetter |
  QuotedDigit | QuotedNonAlphaNum | CharCode
  ;


/* Non-Terminal Definition */

nonTerminalDef :=
  NonTerminalIdent ':=' expression
  ;


/* Expression */

expression :=
  term ( '|' term )*
  ;


/* Term */

term :=
  simpleTerm+
  ;


/* Simple Term */

simpleTerm :=
  factor ( '*' | '+' | '?' )? | literalOrRange
  ;


/* Factor */

factor :=
  NonTerminalIdent | TerminalIdent | ReswordIdent | '(' expression ')'
  ;


/* Terminal Definition */

terminalDef :=
  TerminalIdent ':=' ( terminalExpression | '<platform dependent>' )
  ;


/* Terminal Expression */

terminalExpression :=
 terminalTerm ( '|' terminalTerm )*
 ;


/* Terminal Term */

terminalTerm :=
  simpleTerminalTerm+
  ;


/* Simple Terminal Term */

simpleTerminalTerm :=
  terminalFactor ( '*' | '+' | '?' )? | literalOrRange
  ;


/* Terminal Factor */

terminalFactor :=
  TerminalIdent | '(' terminalExpression ')'
  ;


/* Terminal Fragment Definition */

fragmentDef :=
  '.' terminalDef
  ;


/* Non-Semantic Symbol Definition */

nonSemanticDef :=
  '*' terminalDef
  ;


/* Literal Or Range Thereof */

literalOrRange :=
  String | quotedLowerLetterOrRange | quotedUpperLetterOrRange |
  quotedDigitOrRange | charCodeOrRange | QuotedAlphaNum
  ;


/* Quoted Lowercase Letter Or Range Thereof */

quotedLowerLetterOrRange :=
  QuotedLowerLetter ( '..' QuotedLowerLetter )?
  ;


/* Quoted Uppercase Letter Or Range Thereof */

quotedUpperLetterOrRange :=
  QuotedUpperLetter ( '..' QuotedUpperLetter )?
  ;


/* Quoted Decimal Digit Or Range Thereof */

quotedDigitOrRange :=
  QuotedDigit ( '..' QuotedDigit )?
  ;


/* Character Code Or Range Thereof */

charCodeOrRange :=
  CharCode ( '..' CharCode )?
  ;


/*** Terminal Symbols ***/

/* Non-Terminal Identifier */

NonTerminalIdent :=
  LowerLetter ( LowerLetter | UpperLetter | Digit )*
  ;


/* Terminal Identifier */

TerminalIdent :=
   UpperLetter ( LowerLetter | UpperLetter | Digit )*
  ;


/* Reserved Word Identifier */

ReswordIdent :=
   UpperLetter+
  ;


/* Lowercase Letter */

.LowerLetter := 'a' .. 'z' ;


/* Uppercase Letter */

.UpperLetter := 'A' .. 'Z' ;


/* Decimal Digit */

.Digit := '0' .. '9' ;


/* String Literal */

String :=
  "'" Character+ "'" | '"' Character+ '"'
  ;


/* Quotable Character */

.Character := 0u20 .. 0u7E ;


/* Quoted Lowercase Letter */

QuotedLowerLetter :=
  '"' LowerLetter '"' | "'" LowerLetter "'"
  ;


/* Quoted Uppercase Letter */

QuotedUpperLetter :=
  '"' UpperLetter '"' | "'" UpperLetter "'"
  ;


/* Quoted Decimal Digit */

QuotedDigit :=
  "'" Digit "'" | '"' Digit '"'
  ;


/* Quoted Non-Alphanumeric Printable */

QuotedNonAlphaNum :=
  "'" NonAlphaNum "'" | '"' NonAlphaNum '"'
  ;


/* Non-Alphanumeric Printable */

.NonAlphaNum :=
  ' ' | /* Space */
  '!' | '#' | '$' | '%' | '&' | '(' | ')' | '*' | '+' | ',' |
  '-' | '.' | '/' | ':' | ';' | '<' | '=' | '>' | '?' | '@' |
  '[' | '\' | ']' | '^' | '_' | '`' | '{' | '|' | '}' | '~'
  ;


/* Character Code Literal */

CharCode :=
  '0u' ( Digit | 'A' .. 'F' )+
  ;


/* End of File */

EndOfFile := <platform dependent>;


/*** Non-Semantic Symbols ***/

/* Block Comment */

*BlockComment :=
  '/*' ( Printable | Tab | Whitespace | Comment | EndOfLine )* '*/'
  ;

/* Printable */

.Printable := 0u20 .. 0u7E;


/* Tabulator */

.Tab := 0u9;


/* Whitespace */

.Whitespace := 0u20;


/* End of Line */

.EndOfLine :=
  CR LF? | LF
  ;

.CR := 0uD;

.LF := 0uA;


endg gll.

/* END OF FILE */
